buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    // https://plugins.gradle.org/plugin/org.springframework.boot
    id 'org.springframework.boot' version '2.1.6.RELEASE' // TODO use "${SPRING_BOOT_VERSION}" when gradle 5.6.0 released.
    // https://plugins.gradle.org/plugin/io.spring.dependency-management
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'java'
}

group = 'uk.me.jeremygreen'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencyLocking {
    lockAllConfigurations()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${SPRING_BOOT_VERSION}"
    runtimeOnly "org.springframework.boot:spring-boot-devtools:${SPRING_BOOT_VERSION}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${SPRING_BOOT_VERSION}"
}

// Add React frontend artifacts to the jar.
bootJar.dependsOn('src:frontend:npm_run_build')
bootJar {
    from("${project('src:frontend').projectDir}/build") {
	into("public")
    }
}

test.dependsOn('src:frontend:npm_run_ci')
